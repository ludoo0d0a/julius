import com.android.build.api.dsl.ApplicationExtension
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.text.SimpleDateFormat
import java.util.Date
import java.util.Locale
import java.util.Properties

plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hotswan.compiler)
    alias(libs.plugins.kotlinAndroid)
}

configure<ApplicationExtension> {
    namespace = "fr.geoking.julius"
    compileSdk = 36

    defaultConfig {
        applicationId = "fr.geoking.julius"
        minSdk = 26
        targetSdk = 35
        val ciRunNumber = System.getenv("GITHUB_RUN_NUMBER")?.toIntOrNull()
        val ciRunAttempt = System.getenv("GITHUB_RUN_ATTEMPT")?.toIntOrNull() ?: 1
        val localProps = rootProject.file("local.properties").takeIf { it.exists() }?.let { file ->
            Properties().apply { file.inputStream().use { load(it) } }
        } ?: Properties()
        // Keys: local.properties first, then env (CI must set env on the step that runs Gradle, e.g. JULES_KEY, GOOGLE_MAPS_KEY)
        fun prop(key: String, default: String = "") =
            localProps.getProperty(key) ?: System.getenv(key) ?: default
        // Sanitize for Java string literal: trim, strip newlines, escape backslash and double-quote
        fun sanitizeBuildConfigString(s: String): String =
            s.trim().replace("\\", "\\\\").replace("\"", "\\\"").replace(Regex("[\r\n]+"), " ")
        val localVersionCode = prop("VERSION_CODE").takeIf { it.isNotEmpty() }?.toIntOrNull()
        val computedVersionCode = when {
            ciRunNumber != null -> (ciRunNumber * 10) + ciRunAttempt
            localVersionCode != null -> localVersionCode
            else -> 2
        }
        val computedVersionName = if (ciRunNumber != null) {
            "1.0.$ciRunNumber"
        } else {
            "1.0"
        }
        versionCode = computedVersionCode
        versionName = computedVersionName
        buildConfigField("int", "VERSION_CODE", "$computedVersionCode")
        buildConfigField("String", "VERSION_NAME", "\"$computedVersionName\"")
        val buildDate = SimpleDateFormat("yyyy-MM-dd", Locale.US).format(Date())
        buildConfigField("String", "BUILD_DATE", "\"$buildDate\"")

        val elevenLabsKey = sanitizeBuildConfigString(prop("ELEVENLABS_KEY"))
        val geminiKey = sanitizeBuildConfigString(prop("GEMINI_KEY"))
        val deepgramKey = sanitizeBuildConfigString(prop("DEEPGRAM_KEY"))
        val openaiKey = sanitizeBuildConfigString(prop("OPENAI_KEY"))
        val perplexityKey = sanitizeBuildConfigString(prop("PERPLEXITY_KEY"))
        val firebaseAiKey = sanitizeBuildConfigString(prop("FIREBASE_AI_KEY"))
        val firebaseAiModel = sanitizeBuildConfigString(prop("FIREBASE_AI_MODEL", "gemini-1.5-flash-latest"))
        val opencodeZenKey = sanitizeBuildConfigString(prop("OPENCODE_ZEN_KEY"))
        val completionsMeKey = sanitizeBuildConfigString(prop("COMPLETIONS_ME_KEY"))
        val apifreellmKey = sanitizeBuildConfigString(prop("APIFREELLM_KEY"))
        val deepseekKey = sanitizeBuildConfigString(prop("DEEPSEEK_KEY"))
        val groqKey = sanitizeBuildConfigString(prop("GROQ_KEY"))
        val openrouterKey = sanitizeBuildConfigString(prop("OPENROUTER_KEY"))
        val julesKey = sanitizeBuildConfigString(prop("JULES_KEY"))
        val githubToken = sanitizeBuildConfigString(prop("GITHUB_TOKEN"))
        val googleWebClientId = sanitizeBuildConfigString(prop("GOOGLE_WEB_CLIENT_ID", "your_web_client_id_placeholder"))
        val mobiliteitLuxembourgKey = sanitizeBuildConfigString(prop("MOBILITEIT_LUXEMBOURG_KEY"))
        val tomtomKey = sanitizeBuildConfigString(prop("TOMTOM_KEY"))
        val mapsApiKey = prop("GOOGLE_MAPS_KEY")
        manifestPlaceholders["googleMapsApiKey"] = mapsApiKey

        buildConfigField("String", "ELEVENLABS_KEY", "\"$elevenLabsKey\"")
        buildConfigField("String", "GOOGLE_WEB_CLIENT_ID", "\"$googleWebClientId\"")
        buildConfigField("String", "JULES_KEY", "\"$julesKey\"")
        buildConfigField("String", "GITHUB_TOKEN", "\"$githubToken\"")
        buildConfigField("String", "GEMINI_KEY", "\"$geminiKey\"")
        buildConfigField("String", "DEEPGRAM_KEY", "\"$deepgramKey\"")
        buildConfigField("String", "OPENAI_KEY", "\"$openaiKey\"")
        buildConfigField("String", "PERPLEXITY_KEY", "\"$perplexityKey\"")
        buildConfigField("String", "FIREBASE_AI_KEY", "\"$firebaseAiKey\"")
        buildConfigField("String", "FIREBASE_AI_MODEL", "\"$firebaseAiModel\"")
        buildConfigField("String", "OPENCODE_ZEN_KEY", "\"$opencodeZenKey\"")
        buildConfigField("String", "COMPLETIONS_ME_KEY", "\"$completionsMeKey\"")
        buildConfigField("String", "APIFREELLM_KEY", "\"$apifreellmKey\"")
        buildConfigField("String", "DEEPSEEK_KEY", "\"$deepseekKey\"")
        buildConfigField("String", "GROQ_KEY", "\"$groqKey\"")
        buildConfigField("String", "OPENROUTER_KEY", "\"$openrouterKey\"")
        buildConfigField("String", "MOBILITEIT_LUXEMBOURG_KEY", "\"$mobiliteitLuxembourgKey\"")
        buildConfigField("String", "TOMTOM_KEY", "\"$tomtomKey\"")

        // Required for Google Play Services Maps (references legacy Apache HTTP classes removed from Android 9+)
        useLibrary("org.apache.http.legacy")
    }

    buildTypes {
        debug {
            isDebuggable = true
            isMinifyEnabled = false
            isShrinkResources = false
        }
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }



    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }


    packaging {
        resources {
            excludes += "META-INF/INDEX.LIST"
            excludes += "META-INF/DEPENDENCIES"
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

}

// Fichier de désobscurcissement (mapping R8) associé à l’App Bundle.
// Après un bundle*Release, le mapping est copié dans build/deobfuscation/
// pour upload Play Console ou crash reporting.
afterEvaluate {
    val appExt = extensions.getByType(com.android.build.api.dsl.ApplicationExtension::class.java)
    val versionName = appExt.defaultConfig.versionName ?: "unknown"
    val copyMappings = tasks.register("copyReleaseMappings") {
        doLast {
            val buildDir = layout.buildDirectory.get().asFile
            val mappingRoot = buildDir.resolve("outputs/mapping")
            val destDir = buildDir.resolve("deobfuscation")
            if (!mappingRoot.isDirectory) return@doLast
            mappingRoot.listFiles()?.filter { it.isDirectory }?.forEach { variantDir ->
                val mappingFile = File(variantDir, "mapping.txt")
                if (mappingFile.exists()) {
                    destDir.mkdirs()
                    val dest = File(destDir, "mapping-${variantDir.name}-$versionName.txt")
                    mappingFile.copyTo(dest, overwrite = true)
                    logger.lifecycle("Mapping copié: ${dest.absolutePath}")
                }
            }
        }
    }
    tasks.matching { it.name.startsWith("bundle") && it.name.endsWith("Release") }.configureEach {
        finalizedBy(copyMappings)
    }
}

dependencies {
    implementation(project(":shared"))
    
    // Compose & Activity (lifecycle-runtime ensures LifecycleOwner is on classpath for ComponentActivity)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.tooling.preview)
    implementation(libs.compose.foundation)
    implementation(libs.compose.foundation.layout)
    implementation(libs.compose.material3)
    implementation(libs.compose.material.icons.core)
    implementation(libs.compose.material.icons.extended)
    implementation(libs.androidx.core.ktx)
    debugImplementation(libs.compose.ui.tooling)

    // Android Auto
    implementation(libs.androidx.car.app)
    implementation(libs.androidx.car.app.projected)

    // Location (replaces deprecated LocationManager.requestSingleUpdate)
    implementation(libs.play.services.location)
    implementation(libs.kotlinx.coroutines.play.services)

    // Maps
    implementation(libs.maps.compose)
    // Bundle Apache HTTP legacy classes for Play Services Maps Dynamite (removed from Android 9+ bootclasspath)
    implementation(libs.httpclient.android)

    // Media3 for Dashboard Tile
    implementation(libs.media3.session)
    implementation(libs.media3.common)
    implementation(libs.guava)

    // Ktor
    implementation(libs.ktor.client.core)
    implementation(libs.ktor.client.okhttp)
    implementation(libs.ktor.client.content.negotiation)
    implementation(libs.ktor.serialization.kotlinx.json)

    // DI
    implementation(libs.koin.android)

    // Play In-App Update (warns when update available; flexible flow)
    implementation(libs.play.app.update)
    implementation(libs.play.app.update.ktx)

    // Google Auth / Credentials
    implementation(libs.androidx.credentials)
    implementation(libs.androidx.credentials.play.services.auth)
    implementation(libs.googleid)

    // Room
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    ksp(libs.androidx.room.ksp)

    // Vosk offline STT (car mic path)
    implementation(libs.vosk.android)

    // Coil for loading API logos in About
    implementation("io.coil-kt.coil3:coil-compose:${libs.versions.coil.get()}")
    implementation("io.coil-kt.coil3:coil-network-okhttp:${libs.versions.coil.get()}")

    testImplementation(libs.kotlin.test)
    testImplementation(libs.kotlin.test.junit)
    testImplementation(libs.robolectric)
}
android {
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }
}

